
plugins {
    id 'com.jfrog.bintray' version '1.6'
    id 'com.github.jruby-gradle.base' version '1.2.1'
    id 'java'
    id 'checkstyle'
}
import com.github.jrubygradle.JRubyExec

def bintrayReleaseProjects = [
    project(":embulk-input-jdbc"),
    project(":embulk-input-mysql"),
    project(":embulk-input-postgresql"),
    project(":embulk-input-redshift"),
    project(":embulk-input-oracle"),
    project(":embulk-input-sqlserver"),
    project(":embulk-input-db2"),
]

allprojects {
    group = 'org.embulk.input.jdbc'
    version = '0.8.4'

    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    bintray {
        // write at your bintray user name and api key to ~/.gradle/gradle.properties file:
        // bintray_user=frsyuki
        // bintray_api_key=xxxxxxxxxxx
        user = project.hasProperty('bintray_user') ? bintray_user : ''
        key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''

        publications = ['bintrayMavenRelease']

        dryRun = !bintrayReleaseProjects.contains(project)
        publish = true

        pkg {
            userOrg = 'embulk-input-jdbc'
            repo = 'maven'
            name = project.name
            desc = 'MySQL, PostgreSQL, Redshift and generic JDBC input plugins for Embulk'
            websiteUrl = 'https://github.com/embulk/embulk-input-jdbc'
            issueTrackerUrl = 'https://github.com/embulk/embulk-input-jdbc/issues'
            vcsUrl = 'https://github.com/embulk/embulk-input-jdbc.git'
            licenses = ['Apache-2.0']
            labels = ['embulk', 'java']
            publicDownloadNumbers = true

            version {
                name = project.version
            }
        }
    }
}

subprojects {
    apply plugin: 'maven'  // install jar files to the local repo: $ gradle install
    apply plugin: 'java'
    apply plugin: 'findbugs'
    //apply plugin: 'jacoco'
    apply plugin: 'com.github.jruby-gradle.base'

    repositories {
        mavenCentral()
        jcenter()
    }

    configurations {
        provided
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        compile  'org.embulk:embulk-core:0.8.15'
        provided 'org.embulk:embulk-core:0.8.15'
        testCompile 'org.embulk:embulk-test:0.8.15'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" //<< "-Xlint:deprecation"
        options.encoding = 'UTF-8'
    }
    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    findbugs {
        ignoreFailures = true
    }

    javadoc {
        options {
            locale = 'en_US'
            encoding = 'UTF-8'
        }
    }

    test {
        // JDBC input plugins depend on local time zone to parse timestamp without time stamp and datetime types.
        jvmArgs "-Duser.country=FI", "-Duser.timezone=Europe/Helsinki"
        environment "TZ", "Europe/Helsinki"
    }

    task classpath(type: Copy, dependsOn: ["jar"]) {
        doFirst { file('classpath').deleteDir() }
        from (configurations.runtime - configurations.provided + files(jar.archivePath))
        into 'classpath'
    }
    clean { delete 'classpath' }

    checkstyle {
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        toolVersion = '6.14.1'
    }
    checkstyleMain {
        configFile = file("${project.rootDir}/config/checkstyle/default.xml")
        ignoreFailures = true
    }
    checkstyleTest {
        configFile = file("${project.rootDir}/config/checkstyle/default.xml")
        ignoreFailures = true
    }
    task checkstyle(type: Checkstyle) {
        classpath = sourceSets.main.output + sourceSets.test.output
        source = sourceSets.main.allJava + sourceSets.test.allJava
    }

    task gem(type: JRubyExec, dependsOn: ['build', 'gemspec', 'classpath']) {
        jrubyArgs '-rrubygems/gem_runner', "-eGem::GemRunner.new.run(ARGV)", 'build'
        scriptArgs "${project.projectDir.absolutePath}/build/gemspec"
        doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "${parent.projectDir}/pkg") }
    }

    javadoc {
        options {
            locale = 'en_US'
            encoding = 'UTF-8'
        }
    }

    // add tests/javadoc/source jar tasks as artifacts to be released
    task testsJar(type: Jar, dependsOn: classes) {
        classifier = 'tests'
        from sourceSets.test.output
    }
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives testsJar, sourcesJar, javadocJar
    }

    publishing {
        publications {
            bintrayMavenRelease(MavenPublication) {
                from components.java
                artifact testsJar
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    task gemspec << {
        file('build').mkdirs();
        file('build/gemspec').write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["Sadayuki Furuhashi"]
  spec.summary       = %[JDBC input plugin for Embulk]
  spec.description   = %[Selects records from a table.]
  spec.email         = ["frsyuki@gmail.com"]
  spec.licenses      = ["Apache 2.0"]
  spec.homepage      = "https://github.com/embulk/embulk-input-jdbc"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]
end
/$)
    }
}

task gemPush << {
    "gem push pkg/embulk-input-jdbc-${project.version}.gem".execute().waitFor()
    "gem push pkg/embulk-input-mysql-${project.version}.gem".execute().waitFor()
    "gem push pkg/embulk-input-postgresql-${project.version}.gem".execute().waitFor()
    "gem push pkg/embulk-input-redshift-${project.version}.gem".execute().waitFor()
    "gem push pkg/embulk-input-oracle-${project.version}.gem".execute().waitFor()
    "gem push pkg/embulk-input-sqlserver-${project.version}.gem".execute().waitFor()
    "gem push pkg/embulk-input-db2-${project.version}.gem".execute().waitFor()
}
